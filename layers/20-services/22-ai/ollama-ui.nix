{
  config,
  lib,
  pkgs,
  ...
}:
with lib;
let
  cfg = config.services.ai-services.ollama-ui;
in
{
  options.services.ai-services.ollama-ui = {
    enable = mkOption {
      type = types.bool;
      default = false;
      description = "Enable NextJS Ollama LLM UI";
    };

    port = mkOption {
      type = types.int;
      default = 3004;
      description = "Ollama UI port";
    };
  };

  config = mkIf cfg.enable {
    users.users.nextjs-ollama-llm-ui = {
      group = "nextjs-ollama-llm-ui";
      isSystemUser = true;
      description = "NextJS Ollama LLM UI Service User";
      home = "/var/lib/nextjs-ollama-llm-ui";
      createHome = true;
    };
    users.groups.nextjs-ollama-llm-ui = { };

    systemd.services.nextjs-ollama-llm-ui = {
      description = "NextJS Ollama LLM UI";
      wantedBy = [ "multi-user.target" ];
      after = [
        "network.target"
        "ollama.service"
      ];
      serviceConfig = {
        ExecStart = "${pkgs.nextjs-ollama-llm-ui}/bin/nextjs-ollama-llm-ui";
        User = "nextjs-ollama-llm-ui";
        Group = "nextjs-ollama-llm-ui";
        WorkingDirectory = "/var/lib/nextjs-ollama-llm-ui";
        Environment = [
          "PORT=${toString cfg.port}"
          "OLLAMA_URL=http://localhost:11434"
        ];
      };
    };

    networking.firewall.allowedTCPPorts = [ cfg.port ];

    environment.persistence."/persist" = mkIf (config.layers.layer-10.system.config.impermanence.enable or false) {
      directories = [
        {
          directory = "/var/lib/nextjs-ollama-llm-ui";
          user = "nextjs-ollama-llm-ui";
          group = "nextjs-ollama-llm-ui";
          mode = "0750";
        }
      ];
    };
  };
}